# Copyright (c) 2021 Robert Bosch Manufacturing Solutions GmbH
#
# See the AUTHORS file(s) distributed with this work for additional
# information regarding authorship.
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.
#
# SPDX-License-Identifier: MPL-2.0

prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix bamm: <urn:bamm:io.openmanufacturing:meta-model:1.0.0#>
prefix unit: <urn:bamm:io.openmanufacturing:unit:1.0.0#>

select distinct ?unitName ?label ?symbol ?code ?referenceUnitName ?conversionFactor ?quantityKinds
where {
  ?unit rdf:type/rdfs:subClassOf* unit:Unit .
  ?unit bamm:name ?unitName .
  optional {
    ?unit bamm:preferredName ?preferredName .
    bind( str( ?preferredName ) as ?label )
  }
  optional {
    ?unit unit:symbol ?symbol
  }
  optional {
    ?unit unit:commonCode ?code
  }
  optional {
    ?unit unit:referenceUnit ?referenceUnit .
    ?referenceUnit bamm:name ?referenceUnitName .
    ?unit unit:conversionFactor ?conversionFactor .
  }
  optional {
    select ?unitName ( group_concat( ?quantityKindName; separator="," ) as ?quantityKinds )
    where {
      ?unit bamm:name ?unitName .
      ?unit unit:referenceUnit?/unit:quantityKind ?quantityKind .
      ?quantityKind bamm:name ?quantityKindName .
    }
    group by ?unitName
  }
}
order by ?unitName
