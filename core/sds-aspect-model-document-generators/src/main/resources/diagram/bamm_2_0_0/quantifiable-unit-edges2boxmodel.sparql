# Copyright (c) 2021 Robert Bosch Manufacturing Solutions GmbH
#
# See the AUTHORS file(s) distributed with this work for additional
# information regarding authorship.
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.
#
# SPDX-License-Identifier: MPL-2.0

prefix bamm: <urn:bamm:io.openmanufacturing:meta-model:2.0.0#>
prefix bamm-c: <urn:bamm:io.openmanufacturing:characteristic:2.0.0#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix : <urn:bamm:io.openmanufacturing:meta-model:2.0.0/boxmodel#>

# Generates edges between Quantifiables and Units (unit)
construct {
  ?edge a :Edge .
  ?edge :title "unit" .
  ?edge :from ?from .
  ?edge :to ?to .
} where {
  ?quantifiable bamm-c:unit ?unit .
  ?property rdf:type bamm:Property .
  filter( exists { ?property bamm:characteristic/( bamm-c:baseCharacteristic* | rdfs:subClassOf* ) ?quantifiable } )
  ?quantifiable bamm:name ?quantifiableName .
  bind( replace( str( ?unit ), "[^#]*#(.*)", "$1") as ?unitName )
  bind( concat( ucase( substr( ?unitName, 1, 1 ) ), substr( ?unitName, 2 ) ) as ?upperUnitName )

  bind( concat( ?quantifiableName, "Characteristic" ) as ?quantifiableBoxName )
  bind( iri( concat( str( : ), ?quantifiableBoxName ) ) as ?from )
  bind( concat( ?upperUnitName, "Unit" ) as ?unitBoxName )
  bind( iri( concat( str( : ), ?unitBoxName ) ) as ?to )

  bind( iri( concat( str( : ), ?quantifiableBoxName, "_To_", ?unitBoxName ) ) as ?edge )
}

