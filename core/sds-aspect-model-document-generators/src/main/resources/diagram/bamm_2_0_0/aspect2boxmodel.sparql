# Copyright (c) 2021 Robert Bosch Manufacturing Solutions GmbH
#
# See the AUTHORS file(s) distributed with this work for additional
# information regarding authorship.
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.
#
# SPDX-License-Identifier: MPL-2.0

prefix bamm: <urn:bamm:io.openmanufacturing:meta-model:2.0.0#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix : <urn:bamm:io.openmanufacturing:meta-model:2.0.0/boxmodel#>

# Generates boxes for Aspects
construct {
  ?aspectBox a :Box .
  ?aspectBox :prototype "Aspect" .
  ?aspectBox :title ?aspectName .
  ?aspectBox :entries (
     [
       a :Entry ;
       :title "preferredName" ;
       :text ?preferredName
     ]
     [
       a :Entry ;
       :title "description" ;
       :text ?description
     ]
     [
       a :Entry ;
       :title "see" ;
       :text ?seeValues
     ]
  )
} where {
  ?aspect a bamm:Aspect .
  bind( strafter( str( ?aspect ), "#" ) as ?aspectName ) .

  bind( concat( ?aspectName, "Aspect" ) as ?boxName )
  bind( iri( concat( str( : ), ?boxName ) ) as ?aspectBox )

  optional {
    ?aspect bamm:preferredName ?preferredNameValue .
    filter( lang( ?preferredNameValue ) = "en" )
    bind( xsd:string( ?preferredNameValue ) as ?preferredName )
  }

  optional {
    ?aspect bamm:description ?descriptionValue .
    filter( lang( ?descriptionValue ) = "en" )
    bind( xsd:string( ?descriptionValue ) as ?description )
  }

  #subquery which concatenates all values for the bamm:see attributes
  optional {
    {
      select ( group_concat( ?seeValue; separator=", " ) as ?seeValues ) ?aspect
      where {
        select *
        where {
          ?aspect a bamm:Aspect .
          ?aspect bamm:see ?seeValue .
        }
        order by str( ?seeValue )
      }
      group by ?aspect
    }
  }
}
