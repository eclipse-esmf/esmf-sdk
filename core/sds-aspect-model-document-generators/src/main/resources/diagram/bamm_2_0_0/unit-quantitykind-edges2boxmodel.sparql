# Copyright (c) 2021 Robert Bosch Manufacturing Solutions GmbH
#
# See the AUTHORS file(s) distributed with this work for additional
# information regarding authorship.
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.
#
# SPDX-License-Identifier: MPL-2.0

prefix bamm: <urn:bamm:io.openmanufacturing:meta-model:2.0.0#>
prefix bamm-c: <urn:bamm:io.openmanufacturing:characteristic:2.0.0#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix : <urn:bamm:io.openmanufacturing:meta-model:2.0.0/boxmodel#>

# Generates edges between Units and QuantityKinds (quantityKind)
construct {
  ?edge a :Edge .
  ?edge :title "quantityKind" .
  ?edge :from ?from .
  ?edge :to ?to .
} where {
  [] bamm-c:unit ?unit .
  ?unit bamm:referenceUnit?/bamm:quantityKind ?quantityKind .
  bind( strafter( str( ?unit ), "#" ) as ?unitName ) .
  bind( concat( ucase( substr( ?unitName, 1, 1 ) ), substr( ?unitName, 2 ) ) as ?upperUnitName )
  bind( strafter( str( ?quantityKind ), "#" ) as ?quantityKindGivenName ) .
  bind( concat( ?quantityKindGivenName, "QuantityKind" ) as ?quantityKindName )
  bind( concat( ucase( substr( ?quantityKindName, 1, 1 ) ), substr( ?quantityKindName, 2 ) ) as ?upperQuantityKindName )

  bind( concat( ?upperUnitName, "Unit" ) as ?unitBoxName )
  bind( iri( concat( str( : ), ?unitBoxName ) ) as ?from )
  bind( replace( ?upperQuantityKindName, "-", "" ) as ?quantityKindBoxName )
  bind( iri( concat( str( : ), ?quantityKindBoxName ) ) as ?to )

  bind( iri( concat( str( : ), ?unitBoxName, "_To_", ?quantityKindBoxName ) ) as ?edge )
}

