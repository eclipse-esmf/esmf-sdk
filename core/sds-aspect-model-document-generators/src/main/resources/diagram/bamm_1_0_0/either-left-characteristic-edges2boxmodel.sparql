# Copyright (c) 2021 Robert Bosch Manufacturing Solutions GmbH
#
# See the AUTHORS file(s) distributed with this work for additional
# information regarding authorship.
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.
#
# SPDX-License-Identifier: MPL-2.0

prefix bamm: <urn:bamm:io.openmanufacturing:meta-model:1.0.0#>
prefix bamm-c: <urn:bamm:io.openmanufacturing:characteristic:1.0.0#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix : <urn:bamm:io.openmanufacturing:meta-model:1.0.0/boxmodel#>

# Generates edges between Either Characteristic and its left type Characteristic
construct {
  ?edge a :Edge .
  ?edge :title "left" .
  ?edge :from ?from .
  ?edge :to ?to .
} where {
  ?characteristic rdf:type/rdfs:subClassOf* bamm-c:Either .
  ?characteristic bamm:name ?characteristicName .
  ?characteristic bamm-c:left ?leftTypeCharacteristic .
  ?leftTypeCharacteristic bamm:name ?leftTypeCharacteristicName .

  bind( concat( ?characteristicName, "Characteristic" ) as ?box1Name )
  bind( iri( concat( str( : ), ?box1Name ) ) as ?from )

  bind( concat( ?leftTypeCharacteristicName, "Characteristic" ) as ?box2Name )
  bind( iri( concat( str( : ), ?box2Name ) ) as ?to )

  bind( iri( concat( str( : ), ?box1Name, "_To_", ?box2Name ) ) as ?edge )
}
