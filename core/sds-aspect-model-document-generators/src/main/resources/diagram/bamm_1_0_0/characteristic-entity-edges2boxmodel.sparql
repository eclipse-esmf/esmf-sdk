# Copyright (c) 2021 Robert Bosch Manufacturing Solutions GmbH
#
# See the AUTHORS file(s) distributed with this work for additional
# information regarding authorship.
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.
#
# SPDX-License-Identifier: MPL-2.0

prefix bamm: <urn:bamm:io.openmanufacturing:meta-model:1.0.0#>
prefix bamm-e: <urn:bamm:io.openmanufacturing:entity:1.0.0#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix : <urn:bamm:io.openmanufacturing:meta-model:1.0.0/boxmodel#>

# Generates edges between Characteristic and Entities (dataType)
construct {
  ?edge a :Edge .
  ?edge :title "dataType" .
  ?edge :from ?from .
  ?edge :to ?to .
} where {
  ?characteristic bamm:dataType ?entity .
  filter( ! strstarts( str( ?entity ), str( xsd: ) )
         && ! strstarts( str( ?entity ), str( rdf: ) )
         && ! strstarts( str( ?entity ), str ( bamm-e: ) ) )
  ?characteristic bamm:name ?characteristicName .
  ?entity bamm:name ?entityName .
  bind( concat( ?characteristicName, "Characteristic" ) as ?characteristicBoxName )
  bind( iri( concat( str( : ), ?characteristicBoxName ) ) as ?from )

  bind( concat( ?entityName, "Entity" ) as ?entityBoxName )
  bind( iri( concat( str( : ), ?entityBoxName ) ) as ?to )

  bind( iri( concat( str( : ), ?characteristicBoxName, "_To_", ?entityBoxName ) ) as ?edge )
}

