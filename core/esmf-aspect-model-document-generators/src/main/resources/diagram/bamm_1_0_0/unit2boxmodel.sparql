# Copyright (c) 2021 Robert Bosch Manufacturing Solutions GmbH
#
# See the AUTHORS file(s) distributed with this work for additional
# information regarding authorship.
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.
#
# SPDX-License-Identifier: MPL-2.0

prefix bamm: <urn:samm:org.eclipse.esmf.samm:meta-model:1.0.0#>
prefix bamm-c: <urn:samm:org.eclipse.esmf.samm:characteristic:1.0.0#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix unit: <urn:samm:org.eclipse.esmf.samm:unit:1.0.0#>
prefix : <urn:samm:org.eclipse.esmf.samm:meta-model:1.0.0/boxmodel#>

# Generates boxes for Units
construct {
  ?unitBox a :Box .
  ?unitBox :prototype "Unit" .
  ?unitBox :title ?upperUnitName .
  ?unitBox :entries (
     [
       a :Entry ;
       :title "symbol" ;
       :text ?symbol
     ]
  )
} where {
  ?measurement bamm-c:unit ?unit .
  # Select only those Units that are used in the Aspect
  filter( exists { ?property bamm:characteristic/( bamm-c:baseCharacteristic* | rdfs:subClassOf* ) ?measurement } )
  ?unit bamm:name ?unitName .
  bind( concat( ucase( substr( ?unitName, 1, 1 ) ), substr( ?unitName, 2 ) ) as ?upperUnitName )

  bind( concat( ?upperUnitName, "Unit" ) as ?boxName )
  bind( iri( concat( str( : ), ?boxName ) ) as ?unitBox )

  optional {
    ?unit unit:symbol ?symbol
  }
}
