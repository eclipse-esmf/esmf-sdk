# Copyright (c) 2021 Robert Bosch Manufacturing Solutions GmbH
#
# See the AUTHORS file(s) distributed with this work for additional
# information regarding authorship.
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.
#
# SPDX-License-Identifier: MPL-2.0

prefix mmm: <urn:samm:org.eclipse.esmf.samm:meta-meta-model:2.0.0#>
prefix samm: <urn:samm:org.eclipse.esmf.samm:meta-model:2.0.0#>
prefix samm-c: <urn:samm:org.eclipse.esmf.samm:characteristic:2.0.0#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix : <urn:samm:org.eclipse.esmf.samm:meta-model:2.0.0/boxmodel#>
prefix func: <urn:samm:org.eclipse.esmf.samm:function:2.0.0#>

# Generates boxes for Collection Characteristics
construct {
  ?characteristicBox a :Box .
  ?characteristicBox :prototype "Characteristic + Collection" .
  ?characteristicBox :title ?characteristicName .
  ?characteristicBox :modelElement ?characteristic .
  ?characteristicBox :entries (
     [
       a :Entry ;
       :title "preferredName" ;
       :text ?preferredName
     ]
     [
       a :Entry ;
       :title "description" ;
       :text ?description
     ]
     [
       a :Entry ;
       :title "dataType" ;
       :text ?dataType
     ]
     [
       a :Entry ;
       :title "ordered" ;
       :text ?ordered
     ]
     [
       a :Entry ;
       :title "allowDuplicates" ;
       :text ?allowDuplicates
     ]
     [
       a :Entry ;
       :title "see" ;
       :text ?seeValues
     ]
  )
} where {
  ?characteristic rdf:type/rdfs:subClassOf* samm-c:Collection .

  # Apply only to those Characteristics which are used in the Aspect model,
  # i.e. where a directed link from an Aspect to the Characteristic exists.
  # (<>|!<>) matches any RDF property.
  filter( exists {
    ?aspect a samm:Aspect .
    ?aspect (<>|!<>)* ?characteristic .
  } )

  bind( func:getElementName( ?characteristic ) as ?characteristicName )
  bind( concat( ?characteristicName, "Characteristic" ) as ?boxName )
  bind( iri( concat( str( : ), ?boxName ) ) as ?characteristicBox )

  optional {
    ?characteristic samm:preferredName ?preferredNameValue .
    filter( lang( ?preferredNameValue ) = "en" )
    bind( xsd:string( ?preferredNameValue ) as ?preferredName )
  }

  optional {
    ?characteristic samm:description ?descriptionValue .
    filter( lang( ?descriptionValue ) = "en" )
    bind( xsd:string( ?descriptionValue ) as ?description )
  }

  optional {
    ?characteristic samm:dataType ?dataTypeValue .
    filter( strstarts( str( ?dataTypeValue ), str( xsd: ) )
      || strstarts( str( ?dataTypeValue ), str( rdf: ) ) )
    bind( replace( str( ?dataTypeValue ), "[^#]*#(.*)", "$1") as ?dataType )
  }

  # Join the characteristic from the result-set for the query below with the characteristic from this main query
  filter( ?characteristicName = ?name )
  # Query which determines the ordered and allowDuplicates attributes
  {
    select distinct ?name ?ordered ?allowDuplicates
    where {
      # Handles the cases with > 1 edges from Characteristic to Collection
      {
        # Select all collection classes
        ?char rdf:type/rdfs:subClassOf* ?collection1 .
        ?collection1 mmm:ordered [] .

        # Reselect all collection classes
        ?char rdf:type/rdfs:subClassOf* ?collection2 .
        ?collection2 mmm:ordered ?booleanOrdered .
        bind( str( ?booleanOrdered ) as ?ordered )
        ?collection2 mmm:allowDuplicates ?booleanAllowDuplicates .
        bind( str( ?booleanAllowDuplicates ) as ?allowDuplicates )

        # Find only those pairs where left and right are different
        filter( ?collection1 != ?collection2 )

        # Filter to get first level in subclass hierarchy. Only the ordered and allowDuplicates attributes from the
        # top level collection class are relevant. E.g. for the SortedSet the attributes from the SortedSet need
        # to be returned and not the attributes from the Collection which is the SortedSet's super class
        filter( not exists { ?collection1 rdfs:subClassOf ?collection2 } )
        bind( func:getElementName( ?char ) as ?name )
      }

      union

      # Handles the cases with 1 edge from Characteristic to Collection
      {
        ?char rdf:type/mmm:ordered ?booleanOrdered .
        bind( str( ?booleanOrdered ) as ?ordered )
        ?char rdf:type/mmm:allowDuplicates ?booleanAllowDuplicates .
        bind( str( ?booleanAllowDuplicates ) as ?allowDuplicates )
        bind( func:getElementName( ?char ) as ?name )
      }
    }
  }

  #subquery which concatenates all values for the samm:see attributes
  optional {
    {
      select * where {
        ?characteristic rdf:type/rdfs:subClassOf* samm-c:Collection .
        filter( str( ?subQueryCharacteristic ) = str( ?characteristic ) )
        {
          select ?subQueryCharacteristic ( group_concat( ?seeValue; separator=", " ) as ?seeValues )
          where {
            select *
            where {
              ?subQueryCharacteristic rdf:type/rdfs:subClassOf* samm-c:Collection .
              ?subQueryCharacteristic samm:see ?seeValue .
            }
            order by str( ?seeValue )
          }
          group by ?subQueryCharacteristic
        }
      }
    }
  }
}
