# Copyright (c) 2021 Robert Bosch Manufacturing Solutions GmbH
#
# See the AUTHORS file(s) distributed with this work for additional
# information regarding authorship.
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.
#
# SPDX-License-Identifier: MPL-2.0

prefix bamm: <urn:samm:org.eclipse.esmf.samm:meta-model:2.0.0#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix : <urn:samm:org.eclipse.esmf.samm:meta-model:2.0.0/boxmodel#>

# Generate edges between Aspects and Operations
construct {
  ?edge a :Edge .
  ?edge :title "operation" .
  ?edge :from ?from .
  ?edge :to ?to .
} where {
  ?aspect a bamm:Aspect .
  bind( strafter( str( ?aspect ), "#" ) as ?aspectName ) .
  ?operation a bamm:Operation .
  bind( strafter( str( ?operation ), "#" ) as ?operationName ) .
  filter( exists { ?aspect bamm:operations/rdf:rest*/rdf:first ?operation } )

  bind( concat( ?aspectName, "Aspect" ) as ?aspectBoxName )
  bind( iri( concat( str( : ), ?aspectBoxName ) ) as ?from )
  bind( concat( ?operationName, "Operation" ) as ?operationBoxName )
  bind( iri( concat( str( : ), ?operationBoxName ) ) as ?to )

  bind( iri( concat( str( : ), ?aspectBoxName, "_To_", ?operationBoxName ) ) as ?edge )
}
