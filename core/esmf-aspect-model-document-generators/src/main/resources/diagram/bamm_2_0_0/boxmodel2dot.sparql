# Copyright (c) 2021 Robert Bosch Manufacturing Solutions GmbH
#
# See the AUTHORS file(s) distributed with this work for additional
# information regarding authorship.
#
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.
#
# SPDX-License-Identifier: MPL-2.0

prefix bamm: <urn:samm:org.eclipse.esmf.samm:meta-model:2.0.0#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix sh: <http://www.w3.org/ns/shacl#>
prefix : <urn:samm:org.eclipse.esmf.samm:meta-model:2.0.0/boxmodel#>

# Transforms a box model (boxes and edges) into Graphviz/dot statements
select ?dotStatement
where {
  # Boxes
  {
    ?box a :Box .
    ?box :title ?givenTitle .

    optional {
      ?box :modelElement ?modelElement .
      bind( concat( strbefore( str( ?modelElement ), "#" ), "#" ) as ?modelElementRdfNamespace )

      bamm:prefixDeclarations sh:declare ?declaration .
      ?declaration sh:prefix ?rdfPrefix .
      ?declaration sh:namespace ?rdfNamespace .
      filter( str( ?rdfNamespace ) = str( ?modelElementRdfNamespace ) )
      bind( concat( str( ?rdfPrefix ), ":" ) as ?titlePrefix )
    }

    bind( concat( coalesce( ?titlePrefix, "" ), ?givenTitle ) as ?title )

    optional {
      ?box :prototype ?prototype .
    }
    bind( coalesce( concat( " «", ?prototype, "»\\n" ), "" ) as ?prototypeString )

    # Show only boxes that are reachable via an edge from the Aspect
    [] :rootElement ?aspect .
    ?aspectBox a :Box .
    ?aspectBox :modelElement ?aspect .
    filter( exists { ?aspectBox (^:from/:to)* ?box } )

    optional {
      select ?box ( group_concat( ?attribute; separator="" ) as ?attributes )
      where {
        {
          select ?box ?attribute
          where {
            ?box (:entries/rdf:rest*/rdf:first)|(:entry) ?entry .
            ?entry :title ?entryTitle .
            optional {
              ?entry :text ?entryText .
            }
            bind( if( bound( ?entryText ), concat( ?entryTitle, ": ", str( ?entryText ) , "\\l" ) , "" ) as ?attribute )
          }
        }
      }
      group by ?box
    }

    optional {
      ?box :fillcolor ?fillcolor .
    }
    bind( coalesce( concat( ", fillcolor=\\\"", ?fillcolor, "\\\"" ), "" ) as ?fillcolorAttribute )

    bind( replace( str( ?box ), "[^#]*#(.*)", "$1") as ?boxName )
    bind( replace( coalesce( concat( "|", ?attributes ), "" ), "<", "\\\\<" ) as ?attributes0 )
    bind( replace( ?attributes0, ">", "\\\\>" ) as ?attributes1 )
    bind( replace( ?attributes1, "\\{", "\\\\{" ) as ?attributes2 )
    bind( replace( ?attributes2, "\\}", "\\\\}" ) as ?attributes3 )
    bind( replace( ?attributes3, "\\\"", "\\\\\"" ) as ?attributes4 )
    bind( concat( ?boxName, " [label=\"{", ?prototypeString, ?title, ?attributes4, ?fillcolorAttribute, "}\"]" ) as ?dotStatement )
  }

  union

  # Edges
  {
    ?edge a :Edge .
    ?edge :title ?title .
    ?edge :from ?from .
    ?edge :to ?to .

    # Show only boxes that are reachable via an edge from the Aspect
    [] :rootElement ?aspect .
    ?aspectBox a :Box .
    ?aspectBox :modelElement ?aspect .
    filter( exists { ?aspectBox (^:from/:to)* ?to } )

    bind( replace( str( ?from ), "[^#]*#(.*)", "$1") as ?fromBoxName )
    bind( replace( str( ?to ), "[^#]*#(.*)", "$1") as ?toBoxName )

    bind( concat( ?fromBoxName, " -> ", ?toBoxName, " [label=\"", ?title, "\"]" ) as ?dotStatement )
  }
}

